port module Main exposing (..)
import Html exposing (Html, Attribute)
import Html.App as Html
import Html.Attributes
import Html.Events exposing (onInput, on, targetValue)
import String

-- BOOTSTRAP
{-
  , inputs =
    [ onInit
    , onCodeCompiled
    ]
  }
-}

main : Program Never
main =
  Html.program
    { init = ([], Cmd.none)
    , update = update
    , view = view
    , subscriptions = subscriptions
    }

type Msg = NoOp
  | Init (List (String, String))
  | Update Test String
  | CodeCompiled (String, String)


-- INTEROP

port compileCode : (String, String) -> Cmd msg

port codeCompiled : ((String, String) -> msg) -> Sub msg

port init : (List (String, String) -> msg) -> Sub msg

subscriptions : Model -> Sub Msg
subscriptions _ = Sub.batch
  [ codeCompiled CodeCompiled
  , init Init
  ]

-- MODEL

type alias Test =
  { id: String
  , title : String
  , code : String
  , elm : String
  }

type alias Model = List Test


-- UPDATE

compileTest : Test -> Cmd Msg
compileTest test = compileCode (test.id, test.code)

update : Msg -> Model -> (Model, Cmd Msg)
update action model =
  case action of
    Init items ->
      let
        toTest i (title, code) =
          { id = toString i
          , title = title
          , code = code
          , elm = ""
          }
        newModel =
          List.indexedMap toTest items
        eff =
          List.map compileTest newModel
          |> Cmd.batch
      in
        (newModel, eff)

    Update test code ->
      let
        newTest =
          { test | code = code }
        newModel =
          List.map (\t -> if t == test then newTest else t) model
        eff =
          compileTest newTest
      in
        (newModel, eff)

    CodeCompiled (id, elm) ->
      let
        newModel =
          List.map (\t -> if t.id == id then { t | elm = elm } else t) model
      in
        (newModel, Cmd.none)

    NoOp ->
        (model, Cmd.none)

-- VIEW

view : Model -> Html Msg
view model =
  <div>
    <h1>
      Interactive <a href="https://github.com/pzavolinsky/elmx">elmx</a>
      cheatsheet
    </h1>
    {:List.map viewModelItem model}
  </div>

viewModelItem : Test -> Html Msg
viewModelItem test =
  if test.code == "" && String.startsWith "=" test.title
  then viewTitle test
  else viewTest test

viewTitle : Test -> Html Msg
viewTitle test =
  <div class="test-title">
    <h2 class="title">{=(String.dropLeft 1 test.title)}</h2>
  </div>

viewTest : Test -> Html Msg
viewTest test =
  let
    title =
      if test.title == ""
      then <span></span>
      else <h3 class="title bg-primary">{=test.title}</h3>
  in
    <div class="test">
      {title}
      <div class="row">
        <div class="col-sm-6">
          <div class="text-right"><small >elmx</small></div>
          <textarea {onInput (Update test)} rows={testRows test.code}>{=test.code}</textarea>
        </div>
        <div class="col-sm-6">
          <div class="text-right"><small >elm</small></div>
          <textarea readonly="true" rows={testRows test.elm}>{=test.elm}</textarea>
        </div>
      </div>
    </div>

testRows : String -> String
testRows = toString << (+) 1 << List.length << String.split "\n"
